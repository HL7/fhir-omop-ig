/// url = 'http://hl7.org/fhir/uv/omop/StructureMap/EncounterVisitMap'
/// name = 'EncounterVisitMap'
/// title = 'Mapping Encounter resource to VisitOccurrence OMOP Domain'
/// status = 'draft'

uses "http://hl7.org/fhir/StructureDefinition/Encounter" alias Encounter as source
uses "http://hl7.org/fhir/uv/omop/StructureDefinition/VisitOccurrence" alias VisitTable as target

group VisitOccurrence(source src: Encounter, target tgt : VisitTable) {

	//src.id as id -> tgt.visit_occurrence_id = cast(id, "integer");
	
	//src.subject as s -> tgt then {
	//	s.identifier as sid -> tgt then {
	//		sid.value as a -> tgt.person_id = a;
	//	};
	//};
	
	src.class as s -> tgt then {
		s.coding as sc -> tgt then {
			sc.code as a-> tgt.visit_concept_id = a, tgt.visit_source_value = a, tgt.visit_source_concept_id = a;
		};
	};
	
	src.actualPeriod as s -> tgt then {
		s.start as std -> tgt.visit_start_date = cast(std, "date"), tgt.visit_start_datetime = std;
		s.end as ed -> tgt.visit_end_date = cast(ed, "date"), tgt.visit_end_datetime = ed;
	};

	src.admission as s -> tgt then {
		s.admitSource as sa -> tgt then {
			sa.coding as sc -> tgt then {
				sc.code as code -> tgt.admitted_from_concept_id = code, tgt.admitted_from_source_value = code;
			};
		};
	
		s.dischargeDisposition as sd -> tgt then {
			sd.coding as sc -> tgt then {
				sc.code as code -> tgt.discharged_to_concept_id = code, tgt.discharged_to_source_value = code;
			};
		};
	};
	
}